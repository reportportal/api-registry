buildscript {
    ext {
        protobufVersion = '3.21.12'
        protobufPluginVersion = '0.9.2'
        grpcVersion = '1.52.1'
    }

    buildDir = 'src/build/proto/java'
}

plugins {
    id 'java-library'
    id 'com.google.protobuf' version "${protobufPluginVersion}"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly 'jakarta.annotation:jakarta.annotation-api:1.3.5' // Java 9+ compatibility - Do NOT update to 2.0.0
}

sourceSets {
    main {
        proto {
            // In addition to the default 'src/main/proto'
            srcDir 'api/proto/reportportal'
        }
    }
}
protobuf {
    // Configure the protoc executable
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }

    clean {
        delete generatedFilesBaseDir
    }

    // Locate the codegen plugins
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }

    generateProtoTasks {
        all().configureEach { task ->
            task.plugins {
                grpc {}
            }
        }
    }
}